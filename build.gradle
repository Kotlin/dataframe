buildscript {
    ext.kotlin_version = '1.4.0'
    ext.dokka_version = '0.9.17'

    repositories {
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}




plugins { id "com.jfrog.bintray" version "1.8.1" }

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// not neeeded but does not work without
mainClassName = "foo.Bar" // not needed but does not work without

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://kotlin.bintray.com/kotlin-datascience' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-reflect"


    compile "org.apache.commons:commons-csv:1.3"
    compile 'com.beust:klaxon:0.30'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.kotlintest:kotlintest-assertions:3.1.6'
    testCompile 'de.mpicbg.scicomp:kutils:0.11'


    // needed to work around https://youtrack.jetbrains.com/issue/KT-15064
    compileOnly "org.jetbrains.kotlin:kotlin-script-runtime:$kotlin_version"
}


// see https://github.com/Kotlin/dokka/issues/42
task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir

    reportUndocumented = false

    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// http://stackoverflow.com/questions/11474729/how-to-build-sources-jar-with-gradle
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//Minimal dokka configuration:
dokka {
    outputFormat = 'html'
//    outputDirectory = "$buildDir/javadoc"
    outputDirectory = "$projectDir/javadoc"

    // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
    reportUndocumented = false

//    http://kotlinlang.org/docs/reference/kotlin-doc.html#module-and-package-documentation
    includes = ['docs/packages_info.md']
}

group 'org.jetbrains.kotlin'
version '0.0.6'

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
        }
    }
}
if (hasProperty('bintray.user') && hasProperty('bintray.apikey')) {
    bintray {

        // property must be set in ~/.gradle/gradle.properties

        user = getProperty("bintray.user")
        key = getProperty("bintray.apikey")

        publications = ['maven'] //When uploading configuration files

        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true // If version should be auto published after an upload

        pkg {
            repo = 'kotlin-datascience'
            name = 'krangl-typed'
            vcsUrl = 'https://github.com/nikitinas/krangl-typed'
            userOrg = 'kotlin'
            licenses = ['MIT']
            publicDownloadNumbers = true

            //Optional version descriptor
            version {
                if(hasProperty("build.counter")) {
                    name = project.version + "-dev-" + getProperty("build.counter") //Bintray logical version name
                } else {
                    name = project.version + "-local"
                }
                desc = '.'
                released = new Date()
                vcsTag = 'v' + project.version
            }
        }
    }
}