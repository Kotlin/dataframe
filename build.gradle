buildscript {

    repositories {
        maven { url 'https://dl.bintray.com/kotlin/dokka' }
        mavenCentral()

        def repoFolder = new File(jupyterApiTCRepo)
        if(repoFolder.exists()) {
            maven { url repoFolder }
        }
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-jupyter-api-gradle-plugin:$jupyterApiVersion"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.1"
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "org.jetbrains.kotlin.jupyter.api"

// not neeeded but does not work without
mainClassName = "foo.Bar" // not needed but does not work without

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://kotlin.bintray.com/kotlin-datascience' }

    def repoFolder = new File(jupyterApiTCRepo)
    if(repoFolder.exists()) {
        maven { url repoFolder }
    }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    compile "org.apache.commons:commons-csv:1.3"
    compile 'com.beust:klaxon:5.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'io.kotlintest:kotlintest-assertions:3.1.6'
    testCompile 'de.mpicbg.scicomp:kutils:0.11'

    // needed to work around https://youtrack.jetbrains.com/issue/KT-15064
    compileOnly "org.jetbrains.kotlin:kotlin-script-runtime:$kotlin_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// see https://github.com/Kotlin/dokka/issues/42
task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir

    reportUndocumented = false

    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// http://stackoverflow.com/questions/11474729/how-to-build-sources-jar-with-gradle
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//Minimal dokka configuration:
dokka {
    outputFormat = 'html'
//    outputDirectory = "$buildDir/javadoc"
    outputDirectory = "$projectDir/javadoc"

    // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
    reportUndocumented = false

//    http://kotlinlang.org/docs/reference/kotlin-doc.html#module-and-package-documentation
    includes = ['docs/packages_info.md']
}

group 'org.jetbrains.kotlin'

if(hasProperty("build_counter")) {
    version property('version') + "-dev-" + build_counter
}
else if(hasProperty("release")) {
    version property('version')
} else {
    version property('version') + "-dev"
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId projectName
            groupId group

            from components.java
            artifact sourcesJar { classifier "sources" }
        }
    }
}

if (hasProperty('bintray_user') && hasProperty('bintray_key')) {

    bintray {

        // property must be set in ~/.gradle/gradle.properties

        user = bintray_user
        key = bintray_key
        publications = ['maven'] //When uploading configuration files

        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true // If version should be auto published after an upload

        pkg {
            repo = 'kotlin-datascience'
            name = projectName
            vcsUrl = 'https://github.com/nikitinas/dataframe'
            userOrg = 'kotlin'
            licenses = ['MIT']
            publicDownloadNumbers = true

            //Optional version descriptor
            version {
                name = project.version //Bintray logical version name
                desc = '.'
                released = new Date()
                vcsTag = 'v' + project.version
            }
        }
    }
}